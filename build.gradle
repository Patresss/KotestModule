buildscript {
    repositories {
        maven {url "https://jitpack.io"}
    }
    dependencies {
        classpath "com.github.siordache-forks:javafx-gradle-plugin:0.0.9-rc1"
    }
}

plugins {
    id 'application'
    id 'java'
    id 'idea'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'org.beryx.jlink' version '2.19.0'
}

apply plugin: "org.openjfx.javafxplugin"

group 'com.patres'
version '1.0-SNAPSHOT'
mainClassName = 'kotestModule/com.patres.kotest.JavaFXExample'

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"

    testImplementation ("io.kotest:kotest-runner-junit5-jvm:4.0.6") {
        exclude group: 'io.mockk', module: 'mockk'
    }
    testImplementation "io.kotest:kotest-assertions-core-jvm:4.0.6"

    implementation "log4j:log4j:1.2.17"
    implementation "org.slf4j:slf4j-api:1.7.5"
    implementation "org.slf4j:slf4j-log4j12:1.7.5"
}

jlink {
    addExtraDependencies("javafx")
    mergedModule {
        additive = true
    }
}

javafx {
    version = "11.0.2"
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.swing']
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}

test {
    useJUnitPlatform()
}

def runArgsValue =  [
        "--add-opens=java.base/java.lang=opencv",
        "--add-opens=javafx.controls/javafx.scene.control=kotestModule",
        "--add-opens=javafx.graphics/javafx.scene=kotestModule"
]

run {
    jvmArgs = runArgsValue
}
